plugins {
    id 'java'

    id 'com.github.johnrengelman.shadow' version '5.2.0'
    id 'org.jetbrains.kotlin.jvm' version '1.3.61'
}

group 'info.journeymap.journeymap-bukkit'
version '1.0-SNAPSHOT'

sourceCompatibility = 1.8

repositories {
    mavenCentral()

    maven {
        url "https://papermc.io/repo/repository/maven-public/"
    }
}

sourceSets {
    main.java.srcDirs += 'src/main/kotlin/'
    test.java.srcDirs += 'src/test/kotlin/'
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    compileOnly group: 'org.bukkit', name: 'bukkit', version: '1.14.4-R0.1-SNAPSHOT'
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

shadowJar {
    relocate 'org', 'info.journeymap.shaded.org'
    relocate 'kotlin', 'info.journeymap.shaded.kotlin'
}

defaultTasks "clean", "shadowJar"

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier = 'sources'
    from sourceSets.main.java.srcDirs
}

artifacts {
    archives sourcesJar
    archives shadowJar
}

publishing {
    repositories {
        maven {
            name = "JourneyMap"
            url = uri("https://jm.gserv.me/repository/maven-snapshots/")

            credentials {
                username = project.findProperty("journeymap.user") ?: System.getenv("JOURNEYMAP_USER")
                password = project.findProperty("journeymap.password") ?: System.getenv("JOURNEYMAP_PASSWORD")
            }

            version = project.release
        }
    }

    publications {
        maven(MavenPublication) {
            from(components.java)
        }
    }
}